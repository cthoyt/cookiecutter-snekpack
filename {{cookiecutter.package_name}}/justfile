[doc("run unit and integration tests")]
test:
    # See the [dependency-groups] entry in pyproject.toml for "tests"
    just coverage erase
    uv run --group tests --all-extras -m coverage run -p -m pytest
    just coverage combine
    just coverage report
    just coverage html

[doc("remove testing coverage artifacts")]
coverage-clean:
    just coverage erase

[doc("run `coverage` with a given subcommand")]
@coverage command:
    uvx --from coverage[toml] coverage {{command}}

[doc("test that documentation examples run properly")]
doctests:
    #  note that the package name is required for discovery
    uv run --group doctests --all-extras xdoctest -m src/{{ cookiecutter.package_name }}

[doc("test that notebooks can be run to completion")]
treon:
    uv run treon

[doc("format code")]
format:
    #  Note that ruff check should come before ruff format when using --fix (ref: https://github.com/astral-sh/ruff-pre-commit/blob/main/README.md)
    uvx ruff check --fix
    uvx format

[doc("format documentation")]
format-docs:
    # note that this doesn't work with sphinx-click
    # or any other extension that adds extra directives
    # See the [dependency-groups] entry in pyproject.toml for "rstfmt"
    uv run --group format-docs docstrfmt src/ tests/ docs/ --no-docstring-trailing-line
